properties([
    parameters([
        string(name: 'ciSha', defaultValue: 'main',
          description: 'Commit SHA or branch name. ' +
            'For pull requests, enter branch name <code>pr/1234/head</code> ' +
            'or <code>pr/1234/merge</code> for pull request #1234. ' +
            'Defaults to <code>main</code>.'),
    ]),
    buildDiscarder(
      logRotator(
        daysToKeepStr: '90',
        artifactDaysToKeepStr: '90'
      )
    )
])

node(getNodeLabel()) {
  // Use a custom checkout step below, since there are
  // multiple repositories with a particular directory layout.
  skipDefaultCheckout()

  library identifier: 'utils@master', retriever: modernSCM(
    scm: [
      $class: 'GitSCMSource',
      remote: 'https://github.com/RobotLocomotion/drake',
      credentialsId: '5b9a4e7f-b57a-4692-95b1-6cc704c106fd',
      traits: [gitBranchDiscovery()]
    ],
    libraryPath: '.jenkins/utils/'
  )

  stage('test') {
    timeout(600) {
      ansiColor('xterm') {
        timestamps {
          try {
            // Use the CI branch parameter for checkout (defaults to main).
            def scmVars = utils.checkout(params.ciSha)
            utils.doMainBuild(scmVars)
          } finally {
            try {
              utils.addCDashBadge()
            } finally {
              utils.cleanWorkspace()
            }
          }
        }
      }
    }
  }
}

// Returns the node label from the job name.
def getNodeLabel() {
  def pattern = ~/^((linux|mac-arm)-[a-z]+(?:-unprovisioned)?)/
  def match = "${env.JOB_NAME}" =~ pattern

  if (match.find()) {
    return match.group(1)
  }
  else {
    return null
  }
}

load("@bazel_skylib//lib:selects.bzl", "selects")
load("//tools/install:install.bzl", "install", "install_license")
load("//tools/lint:lint.bzl", "add_lint_tests")

config_setting(
    name = "flag_eigen_repo_pkgconfig",
    flag_values = {
        "//tools/flags:eigen_repo": "pkgconfig",
    },
)

config_setting(
    name = "flag_eigen_repo_default",
    flag_values = {
        "//tools/flags:eigen_repo": "default",
    },
)

config_setting(
    name = "flag_public_repo_default_pkgconfig",
    flag_values = {
        "//tools/flags:public_repo_default": "pkgconfig",
    },
)

selects.config_setting_group(
    name = "flag_eigen_repo_default_pkgconfig",
    match_all = [
        ":flag_eigen_repo_default",
        ":flag_public_repo_default_pkgconfig",
    ],
)

selects.config_setting_group(
    name = "use_pkgconfig",
    match_any = [
        ":flag_eigen_repo_pkgconfig",
        ":flag_eigen_repo_default_pkgconfig",
    ],
)

alias(
    name = "eigen",
    actual = select({
        ":use_pkgconfig": "@pkgconfig_eigen",
        "//conditions:default": "@module_eigen//:eigen",
    }),
    visibility = ["//visibility:public"],
)

install(
    name = "install_hdrs",
    targets = select({
        ":use_pkgconfig": [],
        "//conditions:default": ["@module_eigen//:eigen"],
    }),
    hdr_dest = "include",
    guess_hdrs = "PACKAGE",
    allowed_externals = ["@module_eigen//:eigen"],
)

install_license(
    name = "install_license",
    doc_dest = "share/doc/eigen",
    licenses = [
        "@module_eigen//:license.APACHE",
        "@module_eigen//:license.BSD",
        "@module_eigen//:license.MINPACK",
        "@module_eigen//:license.MPL2",
    ],
)

install(
    name = "install",
    visibility = ["//tools/workspace:__pkg__"],
    deps = [
        ":install_hdrs",
        ":install_license",
    ],
)

add_lint_tests()

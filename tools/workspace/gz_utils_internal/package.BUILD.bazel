# -*- python -*-

load(
    "@drake//tools/workspace:cmake_configure_file.bzl",
    "cmake_configure_file",
)
load(
    "@drake//tools/workspace:check_lists_consistency.bzl",
    "check_lists_consistency",
)
load(
    "@drake//tools/install:install.bzl",
    "install",
)
load(
    "@drake//tools/workspace:vendor_cxx.bzl",
    "cc_library_vendored",
)
load("@drake//tools/workspace:generate_file.bzl", "generate_file")

licenses(["notice"])  # Apache-2.0

package(default_visibility = ["//visibility:private"])

cmake_configure_file(
    name = "config",
    src = "include/ignition/utils/config.hh.in",
    out = "include/ignition/utils/config.hh",
    cmakelists = ["CMakeLists.txt"],
    defines = [
        "IGN_DESIGNATION=utils",
        "PROJECT_VERSION_MAJOR=0",
        "PROJECT_VERSION_MINOR=0",
        "PROJECT_VERSION_PATCH=0",
        "PROJECT_VERSION=0.0",
        "PROJECT_VERSION_FULL=0.0.0",
        "PROJECT_NAME_NO_VERSION=ignition-utils",
    ],
)

generate_file(
    name = "include/ignition/utils/Export.hh",
    content = """
#pragma once
// Simplified version of visibility and deprecation macros.
#define IGNITION_UTILS_VISIBLE
#define IGN_DEPRECATED(version) __attribute__ ((__deprecated__))
    """,
)

_MOST_PUBLIC_HDRS = [
    "include/ignition/utils/ImplPtr.hh",
    "include/ignition/utils/SuppressWarning.hh",
    "include/ignition/utils/detail/DefaultOps.hh",
    "include/ignition/utils/detail/ImplPtr.hh",
    "include/ignition/utils/detail/SuppressWarning.hh",
]

check_lists_consistency(
    files = _MOST_PUBLIC_HDRS,
    glob_include = ["include/**/*.hh"],
)

_HDRS = _MOST_PUBLIC_HDRS + [
    "include/ignition/utils/config.hh",
    "include/ignition/utils/Export.hh",
]

cc_library_vendored(
    name = "gz_utils",
    hdrs = _HDRS,
    hdrs_vendored = [
        x.replace("include/ignition/", "drake_src/drake_vendor/ignition/")
        for x in _HDRS
    ],
    edit_include = {
        "ignition/": "drake_vendor/ignition/",
    },
    includes = ["drake_src"],
    visibility = ["//visibility:public"],
)

install(
    name = "install",
    docs = [
        "COPYING",
        "LICENSE",
    ],
    visibility = ["//visibility:public"],
)

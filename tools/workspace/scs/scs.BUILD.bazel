# -*- python -*-

load(
    "@drake//tools/install:install.bzl",
    "cmake_config",
    "install",
    "install_cmake_config",
)

package(default_visibility = ["//visibility:public"])

config_setting(
    name = "darwin",
    values = {"cpu": "darwin"},
    visibility = ["//visibility:private"],
)

config_setting(
    name = "linux",
    values = {"cpu": "k8"},
    visibility = ["//visibility:private"],
)

# TODO(jamiesnape, hongkai-dai): We probably do not need both :scsdir and
# :scsindir. They are unlikely to happily coexist in libdrake.so and currently
# libdrake.so links only to :scsdir.

SCS_HDRS = glob(["include/*.h"]) + ["linsys/amatrix.h"]

# TODO(jamiesnape): Use system AMD and LDL instead of outdated versions in
# linsys/direct/external. These are licensed under the LGPL so only build a
# shared library for :scsdir.

cc_binary(
    name = "libscsdir.so",
    srcs = SCS_HDRS + glob([
        "linsys/*.c",
        "linsys/common.h",
        "linsys/direct/**/*.c",
        "linsys/direct/**/*.h",
        "src/*.c",
    ]),
    defines = ["LAPACK_LIB_FOUND=1"],
    includes = ["include"],
    linkopts = select({
        ":darwin": ["-framework Accelerate"],
        ":linux": ["-lrt"],
        "@//conditions:default": [],
    }),
    linkshared = 1,
    visibility = ["//visibility:private"],
    deps = select({
        ":linux": [
            "@blas",
            "@lapack",
        ],
        "@//conditions:default": [],
    }),
)

cc_library(
    name = "scsdir",
    srcs = [":libscsdir.so"],
    hdrs = SCS_HDRS,
    defines = ["LAPACK_LIB_FOUND=1"],
    includes = ["include"],
)

cc_library(
    name = "scsindir",
    srcs = glob([
        "linsys/*.c",
        "linsys/common.h",
        "linsys/indirect/*.c",
        "linsys/indirect/*.h",
        "src/*.c",
    ]),
    hdrs = SCS_HDRS,
    defines = ["LAPACK_LIB_FOUND=1"],
    includes = ["include"],
    linkopts = select({
        ":darwin": ["-framework Accelerate"],
        ":linux": ["-lrt"],
        "@//conditions:default": [],
    }),
    deps = select({
        ":linux": [
            "@blas",
            "@lapack",
        ],
        "@//conditions:default": [],
    }),
)

cmake_config(
    package = "scs",
    script = "@drake//tools/workspace/scs:scs-create-cps.py",
    version_file = "include/constants.h",
)

install_cmake_config(package = "scs")

install(
    name = "install",
    targets = [
        ":libscsdir.so",
        ":scsindir",
    ],
    hdrs = SCS_HDRS,
    hdr_dest = "include/scs",
    hdr_strip_prefix = ["include"],
    docs = [
        "LICENSE.txt",
        "linsys/direct/external/AMD_README.txt",
        "linsys/direct/external/LDL_README.txt",
    ],
    doc_strip_prefix = ["linsys/direct/external"],
    deps = [":install_cmake_config"],
)

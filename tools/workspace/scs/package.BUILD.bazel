# -*- python -*-

load("@drake//tools/install:install.bzl", "install")

licenses([
    "notice",  # MIT
    "restricted",  # LGPL-2.1+
])

package(default_visibility = ["//visibility:public"])

config_setting(
    name = "darwin",
    values = {"cpu": "darwin"},
    visibility = ["//visibility:private"],
)

config_setting(
    name = "linux",
    values = {"cpu": "k8"},
    visibility = ["//visibility:private"],
)

SCS_HDRS = glob(["include/*.h"]) + ["linsys/amatrix.h"]

# TODO(jamiesnape): Use system AMD and LDL instead of outdated versions in
# linsys/direct/external. These are licensed under the LGPL so only build a
# shared library for :scsdir.

cc_binary(
    name = "libdrake_scsdir.so",
    srcs = SCS_HDRS + glob([
        "linsys/*.c",
        "linsys/common.h",
        "linsys/direct/**/*.c",
        "linsys/direct/**/*.h",
        "src/*.c",
    ]),
    defines = ["USE_LAPACK=1"],
    includes = [
        "include",
        "linsys",
    ],
    linkopts = select({
        ":linux": [
            "-lrt",
            "-Wl,-soname,libdrake_scsdir.so",
        ],
        "@//conditions:default": [],
    }),
    linkshared = 1,
    visibility = ["//visibility:private"],
    deps = [
        "@blas",
        "@lapack",
    ],
)

cc_library(
    name = "scsdir",
    srcs = [":libdrake_scsdir.so"],
    hdrs = SCS_HDRS,
    defines = ["USE_LAPACK=1"],
    includes = ["include"],
)

install(
    name = "install",
    targets = [":libdrake_scsdir.so"],
    docs = [
        "LICENSE.txt",
        "linsys/direct/external/AMD_README.txt",
        "linsys/direct/external/LDL_README.txt",
    ],
    doc_strip_prefix = ["linsys/direct/external"],
)

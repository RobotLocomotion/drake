# -*- mode: python -*-
# vi: set ft=python :

"""
Finds local system NumPy headers and makes them available to be used as a
C/C++ dependency.

Example:
    WORKSPACE:
        load("@drake//tools/workspace/numpy:repo.bzl", "numpy_repository")
        numpy_repository(
            name = "foo",
        )

    BUILD:
        cc_library(
            name = "foobar",
            deps = ["@foo//:numpy"],
            srcs = ["bar.cc"],
        )

Arguments:
    name: A unique name for this rule.
    linux_interpreter_path: Optional interpreter path for the Python runtime in
        the registered Python toolchain on the @platforms//os:linux platform.
        Defaults to the value of LINUX_INTERPRETER_PATH in
        //tools/py_toolchain:interpreter_paths.bzl.
    macos_interpreter_path: Optional interpreter path for the Python runtime in
        the registered Python toolchain on the @platforms//os:osx (macOS)
        platform. Defaults to the value of MACOS_INTERPRETER_PATH in
        //tools/py_toolchain:interpreter_paths.bzl.
"""

load(
    "@drake//tools/workspace/python:repository.bzl",
    "interpreter_path_attrs",
    "repository_python_info",
)

def _impl(repository_ctx):
    python_info = repository_python_info(repository_ctx)

    result = repository_ctx.execute([
        python_info.python,
        "-c",
        "; ".join([
            "import numpy",
            "print(numpy.get_include())",
        ]),
    ])

    if result.return_code != 0:
        fail("Could NOT determine NumPy include", attr = result.stderr)

    source = repository_ctx.path(result.stdout.strip())
    destination = repository_ctx.path("include")
    repository_ctx.symlink(source, destination)

    file_content = """# -*- python -*-

# DO NOT EDIT: generated by numpy_repository()

licenses([
    "notice",  # BSD-2-Clause AND BSD-3-Clause AND MIT AND Python-2.0
    "unencumbered",  # Public-Domain
])

cc_library(
    name = "numpy",
    hdrs = glob(["include/**"]),
    includes = ["include"],
    visibility = ["//visibility:public"],
)
    """

    repository_ctx.file(
        "BUILD.bazel",
        content = file_content,
        executable = False,
    )

numpy_repository = repository_rule(
    _impl,
    attrs = interpreter_path_attrs,
    local = True,
    configure = True,
)

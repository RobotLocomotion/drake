# -*- mode: python -*-
# vi: set ft=python :

"""
Downloads and unpacks a precompiled version of drake-visualizer (a subset of
Director, https://git.io/vNKjq) and makes it available to be used as a
dependency of shell scripts.

Archive naming convention:
    dv-<version>-g<commit>-python-<python version>-qt-<qt version>
        -vtk-<vtk version>-<platform>-<arch>[-<rebuild>]

Build configuration:
    BUILD_SHARED_LIBS=OFF
    CMAKE_BUILD_TYPE=Release
    CMAKE_C_FLAGS=-D_FORTIFY_SOURCE=2 -fstack-protector-strong
    CMAKE_CXX_FLAGS=-D_FORTIFY_SOURCE=2 -fstack-protector-strong
    CMAKE_EXE_LINKER_FLAGS=-Wl,-Bsymbolic-functions -Wl,-z,now -Wl,-z,relro
    CMAKE_MODULE_LINKER_FLAGS=-Wl,-Bsymbolic-functions -Wl,-z,now -Wl,-z,relro
    CMAKE_SHARED_LINKER_FLAGS=-Wl,-Bsymbolic-functions -Wl,-z,now -Wl,-z,relro
    DD_QT_VERSION=5
    USE_EXTERNAL_INSTALL=ON
    USE_LCM=ON
    USE_LCMGL=ON
    USE_SYSTEM_EIGEN=ON
    USE_SYSTEM_LCM=ON
    USE_SYSTEM_LIBBOT=ON
    USE_SYSTEM_VTK=ON

Example:
    WORKSPACE:
        load(
            "@drake//tools/workspace/drake_visualizer:repository.bzl",
            "drake_visualizer_repository",
        )
        drake_visualizer_repository(name = "foo")

    BUILD:
        sh_binary(
            name = "foobar",
            srcs = ["bar.sh"],
            data = ["@foo//:drake_visualizer"],
        )

Argument:
    name: A unique name for this rule.
"""

load("@drake//tools/workspace:os.bzl", "determine_os")

# TODO(jamiesnape): Publish scripts used to create binaries. There will be a CI
# job for developers to build new binaries on demand.
def _impl(repository_ctx):
    os_result = determine_os(repository_ctx)
    if os_result.error != None:
        fail(os_result.error)

    if os_result.is_macos:
        archive = "dv-0.1.0-337-g70c49254-python-2.7.16-qt-5.12.2-vtk-8.2.0-mac-x86_64.tar.gz"  # noqa
        sha256 = "3402f8de5f782235100519da3960544fdced23f72394010f15c294d9462bfc11"  # noqa
    elif os_result.ubuntu_release == "16.04":
        archive = "dv-0.1.0-337-g70c49254-python-2.7.12-qt-5.5.1-vtk-8.2.0-xenial-x86_64.tar.gz"  # noqa
        sha256 = "683209fa47326fb29dda0fe7d13affc36d64fa1cddc9494699ed25b0dffb41a7"  # noqa
    elif os_result.ubuntu_release == "18.04":
        archive = "dv-0.1.0-337-g70c49254-python-2.7.15-qt-5.9.5-vtk-8.2.0-bionic-x86_64.tar.gz"  # noqa
        sha256 = "a4d630e907b97fd2e77d2fb8384693494471c9db97be1eaf481c369854c6d50a"  # noqa
    else:
        fail("Operating system is NOT supported", attr = os_result)

    urls = [
        x.format(archive = archive)
        for x in repository_ctx.attr.mirrors.get("director")
    ]
    root_path = repository_ctx.path("")

    repository_ctx.download_and_extract(urls, root_path, sha256 = sha256)

    file_content = """# -*- python -*-

# DO NOT EDIT: generated by drake_visualizer_repository()

load("@python//:version.bzl", "PYTHON_VERSION")

licenses([
    "notice",  # Apache-2.0 AND BSD-3-Clause AND Python-2.0
    "reciprocal",  # MPL-2.0
    "restricted",  # LGPL-2.1-only AND LGPL-2.1-or-later AND LGPL-3.0-or-later
    "unencumbered",  # Public-Domain
])

# drake-visualizer has the following non-system dependencies in addition to
# those declared in deps:
#   bot2-lcmgl: LGPL-3.0-or-later
#   ctkPythonConsole: Apache-2.0
#   Eigen: BSD-3-Clause AND MPL-2.0 AND Public-Domain
#   LCM: BSD-3-Clause AND LGPL-2.1-only AND LGPL-2.1-or-later
#   Python: Python-2.0
#   PythonQt: LGPL-2.1-only
#   QtPropertyBrowser: LGPL-2.1-only
# TODO(jamiesnape): Enumerate system dependencies.

py_library(
    name = "drake_visualizer_python_deps",
    deps = [
        "@lcmtypes_bot2_core//:lcmtypes_bot2_core_py",
        # TODO(eric.cousineau): Expose VTK Python libraries here for Linux.
        "@lcmtypes_robotlocomotion//:lcmtypes_robotlocomotion_py",
    ],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "lcm_python2",
    srcs = [
        "lib/python2.7/site-packages/lcm/__init__.py",
        "lib/python2.7/site-packages/lcm/_lcm.so",
    ],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "drake_visualizer",
    srcs = glob([
        "lib/libPythonQt.*",
        "lib/libddApp.*",
        "lib/python2.7/site-packages/bot_lcmgl/**/*.py",
        "lib/python2.7/site-packages/director/**/*.py",
        "lib/python2.7/site-packages/director/**/*.so",
        "lib/python2.7/site-packages/urdf_parser_py/**/*.py",
    ]) + [
        "bin/drake-visualizer",
        "share/doc/director/LICENSE.txt",
    ],
    data = [
        ":drake_visualizer_python_deps",
        "@lcm//:libdrake_lcm.so",
        "@vtk",
    ],
    visibility = ["//visibility:public"],
)

PY_VERSION_FILES = PYTHON_VERSION.startswith("3.") and [":lcm_python2"] or []

load("@drake//tools/install:install.bzl", "install_files")
install_files(
    name = "install",
    dest = ".",
    files = [":drake_visualizer"] + PY_VERSION_FILES,
    visibility = ["//visibility:public"],
)
"""

    repository_ctx.file(
        "BUILD.bazel",
        content = file_content,
        executable = False,
    )

drake_visualizer_repository = repository_rule(
    attrs = {
        "mirrors": attr.string_list_dict(),
    },
    implementation = _impl,
)

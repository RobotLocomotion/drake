# -*- python -*-

load(
    "@drake//tools/workspace:cmake_configure_file.bzl",
    "cmake_configure_file",
)
load(
    "@drake//tools/install:install.bzl",
    "install",
)

licenses(["notice"])  # Apache-2.0

package(
    default_visibility = ["//visibility:public"],
)

# Generates osqp_configure.h based on the defines= we want in Drake.
cmake_configure_file(
    name = "configure_file",
    src = "configure/osqp_configure.h.in",
    out = "include/osqp_configure.h",
    defines = [
        "PRINTING",
        "PROFILING",
        # Keep this in sync with 'INT_TYPE' from //tools/workspace/qdldl.
        "DLONG",
        # Don't define 'DFLOAT' unless //tools/workspace/qdldl is also changed.
    ] + select({
        "@drake//tools/cc_toolchain:apple": [
            "IS_MAC",
        ],
        "@drake//tools/cc_toolchain:linux": [
            "IS_LINUX",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:private"],
)

cc_library(
    name = "osqp",
    hdrs = [
        # From ./CMakeLists.txt at `osqp_headers`.
        "include/auxil.h",
        "include/constants.h",
        "include/cs.h",
        "include/ctrlc.h",
        "include/glob_opts.h",
        "include/kkt.h",
        "include/lin_alg.h",
        "include/lin_sys.h",
        "include/osqp.h",
        "include/osqp_configure.h",
        "include/types.h",
        "include/polish.h",
        "include/proj.h",
        "include/scaling.h",
        "include/util.h",
    ],
    srcs = [
        # From ./lin_sys/direct/qdldl/CMakeLists.txt at `amd_sources`.
        "lin_sys/direct/qdldl/amd/include/amd.h",
        "lin_sys/direct/qdldl/amd/include/amd_internal.h",
        "lin_sys/direct/qdldl/amd/include/SuiteSparse_config.h",
        "lin_sys/direct/qdldl/amd/src/amd_1.c",
        "lin_sys/direct/qdldl/amd/src/amd_2.c",
        "lin_sys/direct/qdldl/amd/src/amd_aat.c",
        "lin_sys/direct/qdldl/amd/src/amd_control.c",
        "lin_sys/direct/qdldl/amd/src/amd_defaults.c",
        "lin_sys/direct/qdldl/amd/src/amd_info.c",
        "lin_sys/direct/qdldl/amd/src/amd_order.c",
        "lin_sys/direct/qdldl/amd/src/amd_post_tree.c",
        "lin_sys/direct/qdldl/amd/src/amd_postorder.c",
        "lin_sys/direct/qdldl/amd/src/amd_preprocess.c",
        "lin_sys/direct/qdldl/amd/src/amd_valid.c",
        "lin_sys/direct/qdldl/amd/src/SuiteSparse_config.c",
        # From ./lin_sys/direct/qdldl/CMakeLists.txt at `qdldl_interface_src`.
        "lin_sys/direct/qdldl/qdldl_interface.h",
        "lin_sys/direct/qdldl/qdldl_interface.c",
        # From ./CMakeLists.txt at `osqp_src`.
        "src/auxil.c",
        "src/cs.c",
        "src/ctrlc.c",
        "src/kkt.c",
        "src/lin_alg.c",
        "src/lin_sys.c",
        "src/osqp.c",
        "src/polish.c",
        "src/proj.c",
        "src/scaling.c",
        "src/util.c",
    ],
    includes = [
        "include",
        "lin_sys/direct/qdldl",
        "lin_sys/direct/qdldl/amd/include",
    ],
    copts = [
        "-fvisibility=hidden",
        "-w",
    ],
    linkstatic = 1,
    # Always link all of OSQP.  It includes an implementation of
    # SuiteSparse_malloc internally which it needs to be able to find.
    # Without this flag, in some circumstances applications linking to
    # both OSQP and SCS can end up using the SuiteSparse_malloc from
    # the SCS shared library, which has an incompatible signature.
    # This ends badly.
    alwayslink = 1,
    deps = [
        "@qdldl",
    ],
)

install(
    name = "install",
    docs = [
        "LICENSE",
        "lin_sys/direct/qdldl/amd/LICENSE",
    ],
    doc_strip_prefix = ["lin_sys/direct/qdldl"],
)

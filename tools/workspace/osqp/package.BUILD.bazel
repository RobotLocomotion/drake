# -*- python -*-

load(
    "@drake//tools/workspace:cmake_configure_file.bzl",
    "cmake_configure_file",
)
load(
    "@drake//tools/install:install.bzl",
    "install",
)

licenses(["notice"])  # Apache-2.0

package(
    default_visibility = ["//visibility:public"],
)

config_setting(
    name = "linux",
    values = {"cpu": "k8"},
    visibility = ["//visibility:private"],
)

# Generates glob_opts.h based on the defines= we want in Drake.
cmake_configure_file(
    name = "glob_opts",
    src = "configure/glob_opts.h.in",
    out = "include/glob_opts.h",
    defines = [
        "PRINTING",
        "PROFILING",
        "DLONG",
    ] + select({
        "@drake//tools/cc_toolchain:apple": [
            "IS_MAC",
        ],
        "@drake//tools/cc_toolchain:linux": [
            "IS_LINUX",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:private"],
)

cc_binary(
    name = "libdrake_osqp.so",
    srcs = glob([
        "lin_sys/direct/suitesparse/*.c",
        "lin_sys/direct/suitesparse/*.h",
        "lin_sys/direct/suitesparse/amd/src/*.c",
        "lin_sys/direct/suitesparse/amd/include/*.h",
        "lin_sys/direct/suitesparse/ldl/src/*.c",
        "lin_sys/direct/suitesparse/ldl/include/*.h",
        "include/*.h",
        "src/*.c",
    ]) + ["include/glob_opts.h"],
    includes = [
        "include",
        "lin_sys",
        "lin_sys/direct/pardiso",
        "lin_sys/direct/suitesparse",
        "lin_sys/direct/suitesparse/amd/include",
        "lin_sys/direct/suitesparse/ldl/include",
    ],
    linkopts = select({
        ":linux": ["-Wl,-soname,libdrake_osqp.so"],
        "//conditions:default": [],
    }),
    linkshared = 1,
    visibility = ["//visibility:private"],
)

cc_library(
    name = "osqp",
    srcs = [":libdrake_osqp.so"],
    hdrs = glob([
        "include/*.h",
    ]) + ["include/glob_opts.h"],
    includes = [
        "include",
    ],
)

install(
    name = "install",
    targets = [":libdrake_osqp.so"],
    docs = [
        "LICENSE",
        "lin_sys/direct/suitesparse/ldl/LICENSE",
        "lin_sys/direct/suitesparse/amd/LICENSE",
    ],
    doc_strip_prefix = ["lin_sys/direct/suitesparse"],
)

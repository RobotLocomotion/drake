load("@bazel_skylib//lib:selects.bzl", "selects")
load("//tools/install:install.bzl", "install", "install_license")
load("//tools/lint:lint.bzl", "add_lint_tests")
load("//tools/skylark:cc.bzl", "cc_library")
load("//tools/skylark:drake_cc.bzl", "cc_nolink_library")

config_setting(
    name = "flag_spdlog_repo_disabled",
    flag_values = {
        "//tools/flags:spdlog_repo": "disabled",
    },
)

config_setting(
    name = "flag_spdlog_repo_pkgconfig",
    flag_values = {
        "//tools/flags:spdlog_repo": "pkgconfig",
    },
)

config_setting(
    name = "flag_spdlog_repo_default",
    flag_values = {
        "//tools/flags:spdlog_repo": "default",
    },
)

config_setting(
    name = "flag_public_repo_default_pkgconfig",
    flag_values = {
        "//tools/flags:public_repo_default": "pkgconfig",
    },
)

selects.config_setting_group(
    name = "flag_spdlog_repo_default_pkgconfig",
    match_all = [
        ":flag_spdlog_repo_default",
        ":flag_public_repo_default_pkgconfig",
    ],
)

selects.config_setting_group(
    name = "use_pkgconfig",
    match_any = [
        ":flag_spdlog_repo_pkgconfig",
        ":flag_spdlog_repo_default_pkgconfig",
    ],
)

cc_library(
    name = "spdlog",
    defines = select({
        ":flag_spdlog_repo_disabled": [],
        "//conditions:default": ["HAVE_SPDLOG"],
    }),
    tags = ["nolint"],
    visibility = ["//visibility:public"],
    deps = select({
        ":flag_spdlog_repo_disabled": [],
        ":use_pkgconfig": ["@pkgconfig_spdlog"],
        "//conditions:default": ["@module_spdlog//:spdlog"],
    }),
)

cc_nolink_library(
    name = "hdrs",
    visibility = ["//tools/install/libdrake:__pkg__"],
    deps = ["@spdlog"],
)

install(
    name = "install_hdrs",
    targets = select({
        ":use_pkgconfig": [],
        "//conditions:default": [":hdrs"],
    }),
    hdr_dest = "include",
    hdr_strip_prefix = ["include"],
    guess_hdrs = "ALLOWED_EXTERNALS",
    allowed_externals = ["@module_spdlog//:spdlog"],
)

install_license(
    name = "install_license",
    doc_dest = "share/doc/spdlog",
    licenses = ["@module_spdlog//:license"],
)

install(
    name = "install",
    visibility = ["//tools/workspace:__pkg__"],
    deps = [
        ":install_hdrs",
        ":install_license",
    ],
)

add_lint_tests()

load("@bazel_skylib//lib:selects.bzl", "selects")
load("//tools/install:install.bzl", "install_license")
load("//tools/lint:lint.bzl", "add_lint_tests")
load("//tools/skylark:cc.bzl", "cc_library")

config_setting(
    name = "flag_spdlog_repo_disabled",
    flag_values = {
        "//tools/flags:spdlog_repo": "disabled",
    },
)

config_setting(
    name = "flag_spdlog_repo_pkgconfig",
    flag_values = {
        "//tools/flags:spdlog_repo": "pkgconfig",
    },
)

config_setting(
    name = "flag_spdlog_repo_default",
    flag_values = {
        "//tools/flags:spdlog_repo": "default",
    },
)

config_setting(
    name = "flag_public_repo_default_pkgconfig",
    flag_values = {
        "//tools/flags:public_repo_default": "pkgconfig",
    },
)

selects.config_setting_group(
    name = "flag_spdlog_repo_default_pkgconfig",
    match_all = [
        ":flag_spdlog_repo_default",
        ":flag_public_repo_default_pkgconfig",
    ],
)

selects.config_setting_group(
    name = "use_pkgconfig",
    match_any = [
        ":flag_spdlog_repo_pkgconfig",
        ":flag_spdlog_repo_default_pkgconfig",
    ],
)

cc_library(
    name = "spdlog",
    defines = select({
        ":flag_spdlog_repo_disabled": [],
        "//conditions:default": ["HAVE_SPDLOG"],
    }),
    tags = ["nolint"],
    visibility = ["//visibility:public"],
    deps = select({
        ":flag_spdlog_repo_disabled": [],
        ":use_pkgconfig": ["@pkgconfig_spdlog"],
        "//conditions:default": ["@module_spdlog//:spdlog"],
    }),
)

install_license(
    name = "install",
    doc_dest = "share/doc/spdlog",
    licenses = ["@module_spdlog//:license"],
    visibility = ["//tools/workspace:__pkg__"],
)

add_lint_tests()

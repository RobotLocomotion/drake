# -*- bazel -*-

load("@drake//tools/install:install.bzl", "install")
load("@drake//tools/skylark:cc.bzl", "cc_library")
load(
    "@drake//tools/skylark:drake_cc.bzl",
    "cc_linkonly_library",
)
load(
    "@drake//tools/workspace:cmake_configure_file.bzl",
    "cmake_configure_file",
)
load(
    "@drake//tools/workspace/expat_internal:cmakedefines.bzl",
    "CMAKE_DEFINES",
    "CMAKE_UNDEFINES",
)

licenses(["notice"])  # MIT

package(default_visibility = ["//visibility:private"])

# Group the headers exported by this library.
cc_library(
    name = "hdrs",
    hdrs = ["expat/lib/expat.h"],
)

# Generate expat_config.h.
cmake_configure_file(
    name = "configure_file",
    src = "expat/expat_config.h.cmake",
    out = "expat/expat_config.h",
    defines = CMAKE_DEFINES,
    undefines = CMAKE_UNDEFINES,
    strict = False,
)

# Compile the code (using both its exported headers and its private headers).
cc_library(
    name = "compiled",
    srcs = glob(
        ["expat/lib/*.c"],
        allow_empty = False,
    ),
    hdrs = glob(["expat/lib/*.h"], allow_empty = False) + [
        "expat/expat_config.h",
        "expat/lib/xmltok_impl.c",
        "expat/lib/xmltok_ns.c",
    ],
    copts = [
        "-Wno-all",
        "-fvisibility=hidden",
    ],
    includes = [
        "expat",
        "expat/lib",
    ],
    linkstatic = True,
    deps = [":hdrs"],
)

# Strip the private headers out; we just want the object code.
cc_linkonly_library(
    name = "archive",
    deps = [":compiled"],
)

# Combine the public headers with the object code.
# This does not provide the private headers.
cc_library(
    name = "expat",
    linkstatic = True,
    deps = [
        ":archive",
        ":hdrs",
    ],
    visibility = ["//visibility:public"],
)

install(
    name = "install",
    docs = ["COPYING"],
    visibility = ["//visibility:public"],
)

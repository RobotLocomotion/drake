set(GIT_REVISION HEAD)
set(BUILD_IDENTIFIER unknown)

if(DEFINED ENV{DRAKE_GIT_SHA})
  set(GIT_REVISION "$ENV{DRAKE_GIT_SHA}")
else()
  if(GIT_EXECUTABLE AND EXISTS "${GIT_DIR}")
    execute_process(COMMAND
      "${GIT_EXECUTABLE}" "--git-dir=${GIT_DIR}" rev-parse HEAD
      RESULT_VARIABLE GIT_REV_PARSE_RESULT_VARIABLE
      OUTPUT_VARIABLE GIT_REV_PARSE_OUTPUT_VARIABLE
      OUTPUT_STRIP_TRAILING_WHITESPACE
    )

    if(GIT_REV_PARSE_RESULT_VARIABLE EQUAL 0)
      set(GIT_REVISION "${GIT_REV_PARSE_OUTPUT_VARIABLE}")
      string(SUBSTRING ${GIT_REVISION} 0 8 GIT_REVISION_SHORT)
      set(BUILD_IDENTIFIER git${GIT_REVISION_SHORT})
    endif()
  endif()
endif()

if(DEFINED ENV{DRAKE_VERSION})
  set(DRAKE_VERSION "$ENV{DRAKE_VERSION}")
else()
  string(TIMESTAMP BUILD_TIMESTAMP "%Y%m%d.%H%M%S")
  set(DRAKE_VERSION "0.0.${BUILD_TIMESTAMP}+${BUILD_IDENTIFIER}")
endif()

configure_file(${INPUT_FILE} ${OUTPUT_FILE} @ONLY)

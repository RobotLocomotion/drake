load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:common_settings.bzl", "int_flag", "string_flag")
load("//tools/lint:lint.bzl", "add_lint_tests")
load("//tools/skylark:sh.bzl", "sh_binary")

package(default_visibility = ["//visibility:public"])

# When this is set to "error", a Drake build will promote some warnings of our
# choice to errors. See drake/tools/cc_toolchain/bazel.rc for details. This is
# intended for internal use only, not for users to configure.
string_flag(
    name = "error_severity",
    build_setting_default = "warning",
    values = [
        "warning",
        "error",
    ],
)

config_setting(
    name = "apple",
    constraint_values = ["@platforms//os:osx"],
)

config_setting(
    name = "linux",
    constraint_values = ["@platforms//os:linux"],
)

config_setting(
    name = "error_severity_warning",
    flag_values = {":error_severity": "warning"},
)

config_setting(
    name = "error_severity_error",
    flag_values = {":error_severity": "error"},
)

selects.config_setting_group(
    name = "apple_clang_with_warnings",
    match_all = [
        ":apple",
        "@rules_cc//cc/compiler:clang",
        ":error_severity_warning",
    ],
)

selects.config_setting_group(
    name = "apple_clang_with_errors",
    match_all = [
        ":apple",
        "@rules_cc//cc/compiler:clang",
        ":error_severity_error",
    ],
)

selects.config_setting_group(
    name = "gcc_with_warnings",
    match_all = [
        "@rules_cc//cc/compiler:gcc",
        ":error_severity_warning",
    ],
)

selects.config_setting_group(
    name = "gcc_with_errors",
    match_all = [
        "@rules_cc//cc/compiler:gcc",
        ":error_severity_error",
    ],
)

selects.config_setting_group(
    name = "linux_clang_with_warnings",
    match_all = [
        ":linux",
        "@rules_cc//cc/compiler:clang",
        ":error_severity_warning",
    ],
)

selects.config_setting_group(
    name = "linux_clang_with_errors",
    match_all = [
        ":linux",
        "@rules_cc//cc/compiler:clang",
        ":error_severity_error",
    ],
)

config_setting(
    name = "debug",
    values = {"compilation_mode": "dbg"},
)

# In our developer builds, we use this as an optional hint for configuring
# compiler warnings. It's neither necessary (nor typically set) when Drake
# is installed with CMake or used as a Bazel external. This is intended for
# internal use only, not for users to configure.
#
# TODO(jwnimmer-tri) Ideally, rules_cc would provide this as a toolchain
# attribute that we could query, but it doesn't seem to exist there yet.
int_flag(
    name = "compiler_major",
    build_setting_default = 0,
)

config_setting(
    name = "compiler_major_13",
    flag_values = {":compiler_major": "13"},
)

selects.config_setting_group(
    name = "gcc_13_with_warnings",
    match_all = [
        "@rules_cc//cc/compiler:gcc",
        ":compiler_major_13",
        ":error_severity_warning",
    ],
)

selects.config_setting_group(
    name = "gcc_13_with_errors",
    match_all = [
        "@rules_cc//cc/compiler:gcc",
        ":compiler_major_13",
        ":error_severity_error",
    ],
)

# Capture some bazel cc_toolchain variables into a bash environment file. These
# values reflect the compiler and flags that Bazel's compilation actions will
# use. Note that this might refer to Bazel's compiler wrapper, `cc_wrapper.sh`,
# and thus may only indirectly identify the actual compiler in use. Note also
# that `$(CC)` may refer to the canonicalized location of the compiler; for
# example, on Ubuntu, Clang may be `/usr/lib/llvm-<version>/bin/clang`,
# canonicalized from `/usr/bin/clang-<version>`.
genrule(
    name = "capture_cc",
    outs = ["capture_cc.env"],
    cmd = "cat <<EOF > '$@'\n" +
          "BAZEL_CC=\"$(CC)\"\n" +
          "BAZEL_CC_FLAGS=\"$(CC_FLAGS)\"\n" +
          "EOF",
    toolchains = [
        "@bazel_tools//tools/cpp:cc_flags",
        "@bazel_tools//tools/cpp:current_cc_toolchain",
    ],
    visibility = ["//common:__pkg__"],
)

# Capture bazel's action environment variables into a bash environment file.
# These values reflect ONLY the command-line (or rcfile) `--action_env`
# settings that Bazel uses when running its toolchain configuration, e.g.
# `CC=clang`. This is not necessarily the same as what compiler (or compiler
# wrapper) is actually invoked during a build. Beware that environment
# variables passed like `CC=... bazel ...` are NOT captured by this!
genrule(
    name = "capture_compiler_config",
    outs = ["capture_compiler_config.env"],
    cmd = "cat <<EOF > '$@'\n" +
          "PATH=\"$$PATH\"\n" +
          "CC=\"$${CC:-}\"\n" +
          "CXX=\"$${CXX:-}\"\n" +
          "EOF",
    visibility = ["//common:__pkg__"],
)

# Utility script to indicate which compiler is selected by use of
# `--config={gcc,clang}`. This is intended to be used by the CI infrastructure,
# which needs a way to get this specific information. Unless you are very sure
# you understand the first sentence of this comment, as well as the caveats
# noted in the comment on the above `genrule`, DON'T USE THIS.
sh_binary(
    name = "print_compiler_config",
    srcs = ["print_compiler_config.sh"],
    args = ["$(location :capture_compiler_config.env)"],
    data = [
        ":capture_compiler_config.env",
    ],
)

add_lint_tests()

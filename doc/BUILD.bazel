# -*- python -*-

package(default_visibility = ["//visibility:public"])

load(
    "@drake//tools/skylark:drake_py.bzl",
    "drake_py_binary",
    "drake_py_library",
    "drake_py_unittest",
)
load(
    ":defs.bzl",
    "DEFAULT_BINARY_TAGS",
    "DEFAULT_TEST_TAGS",
)
load("//tools/lint:lint.bzl", "add_lint_tests")

# For maximum browser compatibility these should be at the root of the
# generated website and should not be renamed.
filegroup(
    name = "favicon",
    srcs = [
        # Manual resizing and padding of "assets/drake-dragon.png" to
        # 192x192px.
        "android-chrome.png",
        # Manual resizing and padding of "assets/drake-dragon.png" to
        # 180x180px.
        "apple-touch-icon.png",
        # Minimal configuration for Microsoft browsers. Versions of Edge prior
        # to the change to WebKit always request a file named
        # "browserconfig.xml" whether or not an alternative is specified in the
        # HTML.
        "browserconfig.xml",
        # Apple Safari always requests a file named "favicon.ico" whether or
        # not an alternative favicon is specified in the HTML.
        # Manual resizing and padding of "assets/drake-dragon.png" to 32x32px
        # and conversion to ICO format.
        "favicon.ico",
        # Manual resizing and padding of "assets/drake-dragon.png" to 32x32px.
        "favicon.png",
        # Mask icon for Safari pinned tabs.
        # Manual removal of colors and layers from "assets/drake-dragon.svg"
        # and transformation to a 0 0 16 16 view box.
        "mask-icon.svg",
        # Site manifest primarily for Android and Chrome.
        # https://developer.mozilla.org/en-US/docs/Web/Manifest
        "site.webmanifest",
    ],
)

filegroup(
    name = "images",
    srcs = [
        "images/drake-dragon.png",
        "images/drake-logo.svg",
        "images/drake-logo-white.svg",
        "images/jenkins_bot_reviewable_comment.png",
    ],
)

drake_py_library(
    name = "jekyll_base",
    srcs = ["jekyll_base.py"],
    tags = DEFAULT_BINARY_TAGS,
)

filegroup(
    name = "third_party_data",
    srcs = glob([
        "third_party/**/*",
    ]),
)

filegroup(
    name = "jekyll_data",
    srcs = [
        "_config.yml",
        "index.md",
        "sample_vimrc",
    ] + glob([
        "_includes/*.html",
        "_includes/*.md",
        "_layouts/*.html",
        "_pages/*.md",
        "_release-notes/*.md",
        "assets/**/*.css",
        "assets/**/*.js",
    ]),
    data = [
        ":favicon",
        ":images",
        ":third_party_data",
    ],
)

drake_py_binary(
    name = "gen_jekyll",
    srcs = ["gen_jekyll.py"],
    add_test_rule = 1,
    data = [":jekyll_data"],
    tags = DEFAULT_BINARY_TAGS,
    test_rule_args = ["--out_dir=<test>"],
    test_rule_tags = DEFAULT_TEST_TAGS,
    deps = [
        ":jekyll_base",
    ],
)

drake_py_binary(
    name = "serve_jekyll",
    srcs = ["serve_jekyll.py"],
    data = [":jekyll_data"],
    tags = DEFAULT_BINARY_TAGS,
    deps = [
        ":jekyll_base",
    ],
)

drake_py_binary(
    name = "build",
    srcs = ["build.py"],
    data = [
        ":gen_jekyll",
        "//doc/doxygen_cxx:build",
        "//doc/pydrake:gen_sphinx",
    ],
    tags = DEFAULT_BINARY_TAGS,
    deps = [
        "@bazel_tools//tools/python/runfiles",
    ],
)

# This rule is used by our CI scripts as a single point of entry to ensure that
# all of our manually-tagged documentation binaries can be built successfully.
filegroup(
    name = "manual_binaries",
    srcs = [
        ":build",
        ":gen_jekyll",
        ":serve_jekyll",
        "//doc/doxygen_cxx:build",
        "//doc/pydrake:gen_sphinx",
        "//doc/pydrake:serve_sphinx",
    ],
    tags = ["manual"],
)

# This rule is used by our CI scripts as a single point of entry to ensure that
# all of our manually-tagged documentation tests pass.
test_suite(
    name = "manual_tests",
    tags = ["manual"],
    tests = [
        ":gen_jekyll_test",
        "//doc/pydrake:gen_sphinx_test",
    ],
)

add_lint_tests(
    python_lint_exclude = [
        ":conf.py",
    ],
)

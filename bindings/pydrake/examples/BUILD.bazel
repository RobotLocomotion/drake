# -*- python -*-

load("@drake//tools/install:install.bzl", "install")
load("//tools/lint:lint.bzl", "add_lint_tests")
load(
    "//tools/skylark:pybind.bzl",
    "drake_pybind_library",
    "get_drake_py_installs",
    "get_pybind_package_info",
)
load(
    "//tools/skylark:drake_py.bzl",
    "drake_py_binary",
    "drake_py_library",
    "drake_py_test",
)

package(default_visibility = [
    "//bindings/pydrake:__subpackages__",
])

# This determines how `PYTHONPATH` is configured, and how to install the
# bindings.
PACKAGE_INFO = get_pybind_package_info("//bindings")

# @note Symbols are NOT imported directly into
# `__init__.py` to simplify dependency management, meaning that
# classes are organized by their directory structure rather than
# by C++ namespace.
drake_py_library(
    name = "module_py",
    srcs = ["__init__.py"],
    imports = PACKAGE_INFO.py_imports,
    deps = [
        "//bindings/pydrake:common_py",
    ],
)

drake_pybind_library(
    name = "acrobot_py",
    cc_deps = [
        "//examples/acrobot:acrobot_plant",
    ],
    cc_so_name = "acrobot",
    cc_srcs = ["acrobot_py.cc"],
    package_info = PACKAGE_INFO,
    py_deps = [
        ":module_py",
        "//bindings/pydrake/systems",
    ],
)

drake_pybind_library(
    name = "pendulum_py",
    cc_deps = [
        "//examples/pendulum:pendulum_plant",
    ],
    cc_so_name = "pendulum",
    cc_srcs = ["pendulum_py.cc"],
    package_info = PACKAGE_INFO,
    py_deps = [
        ":module_py",
        "//bindings/pydrake/systems",
    ],
)

PY_LIBRARIES_WITH_INSTALL = [
    ":acrobot_py",
    ":pendulum_py",
]

PY_LIBRARIES = [
    ":module_py",
]

drake_py_library(
    name = "examples",
    imports = PACKAGE_INFO.py_imports,
    deps = PY_LIBRARIES_WITH_INSTALL + PY_LIBRARIES,
)

install(
    name = "install",
    targets = PY_LIBRARIES,
    py_dest = PACKAGE_INFO.py_dest,
    deps = get_drake_py_installs(PY_LIBRARIES_WITH_INSTALL),
)

drake_py_test(
    name = "pendulum_test",
    size = "small",
    deps = [
        ":pendulum_py",
    ],
)

drake_py_test(
    name = "acrobot_test",
    size = "small",
    deps = [
        ":acrobot_py",
    ],
)

add_lint_tests()

# -*- python -*-

load("@drake//tools/install:install.bzl", "install")
load(
    "@drake//tools/skylark:pybind.bzl",
    "drake_pybind_library",
    "get_drake_py_installs",
    "get_pybind_package_info",
)
load(
    "@drake//tools/skylark:drake_py.bzl",
    "drake_py_binary",
    "drake_py_library",
    "drake_py_unittest",
)
load("//bindings/pydrake:pydrake.bzl", "add_lint_tests_pydrake")

package(default_visibility = [
    "//bindings/pydrake:__subpackages__",
])

# This determines how `PYTHONPATH` is configured, and how to install the
# bindings.
PACKAGE_INFO = get_pybind_package_info("//bindings")

drake_py_library(
    name = "module_py",
    srcs = ["__init__.py"],
    imports = PACKAGE_INFO.py_imports,
    deps = [
        "//bindings/pydrake:module_py",
    ],
)

drake_pybind_library(
    name = "planner_py",
    cc_deps = [
        "//bindings/pydrake:documentation_pybind",
        "//bindings/pydrake/common:drake_optional_pybind",
        "//bindings/pydrake/common:eigen_pybind",
    ],
    cc_srcs = ["planner_py.cc"],
    package_info = PACKAGE_INFO,
    py_deps = [
        ":module_py",
    ],
)

drake_py_library(
    name = "simple_ui_py",
    srcs = ["simple_ui.py"],
    imports = PACKAGE_INFO.py_imports,
    deps = [":module_py"],
)

drake_pybind_library(
    name = "robot_plan_runner_py",
    cc_deps = [
        "//bindings/pydrake:documentation_pybind",
        "//bindings/pydrake/common:deprecation_pybind",
        "//bindings/pydrake/common:drake_optional_pybind",
    ],
    cc_srcs = ["robot_plan_runner_py.cc"],
    package_info = PACKAGE_INFO,
    py_deps = [
        ":module_py",
        "//bindings/pydrake/systems",
    ],
    visibility = ["//visibility:public"],
)

PY_LIBRARIES_WITH_INSTALL = [
    ":planner_py",
    ":robot_plan_runner_py",
]

PY_LIBRARIES = [
    ":module_py",
    ":simple_ui_py",
]

drake_py_unittest(
    name = "planner_test",
    data = [
        "//multibody/benchmarks/acrobot:models",
    ],
    deps = [
        ":planner_py",
        "//bindings/pydrake/multibody",
    ],
)

drake_py_unittest(
    name = "robot_plan_runner_test",
    deps = [
        ":robot_plan_runner_py",
    ],
)

drake_py_unittest(
    name = "simple_ui_test",
    data = [
        "//multibody/benchmarks/acrobot:models",
    ],
    deps = [
        ":simple_ui_py",
        "//bindings/pydrake/multibody",
    ],
)

# Symbol roll-up (for user ease).
drake_py_library(
    name = "all_py",
    srcs = ["all.py"],
    deps = PY_LIBRARIES_WITH_INSTALL + PY_LIBRARIES,
)

# Package roll-up (for Bazel dependencies).
drake_py_library(
    name = "manipulation",
    deps = [":all_py"],
)

install(
    name = "install",
    py_dest = PACKAGE_INFO.py_dest,
    targets = PY_LIBRARIES + [":all_py"],
    deps = get_drake_py_installs(PY_LIBRARIES_WITH_INSTALL),
)

add_lint_tests_pydrake()

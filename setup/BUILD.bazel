load("//tools/install:install.bzl", "install_files")
load("//tools/lint:lint.bzl", "add_lint_tests")
load("//tools/skylark:drake_py.bzl", "drake_py_unittest")

package(default_visibility = ["//visibility:public"])

# Make our prerequisites data (but not scripts) available to downstream
# projects.
PREREQUISITES_DATA = glob([
    "**/Brewfile*",
    "**/*.json",
    "**/*.txt",
])

exports_files(PREREQUISITES_DATA)

filegroup(
    name = "prerequisites_data",
    srcs = PREREQUISITES_DATA,
)

filegroup(
    name = "deepnote",
    srcs = [
        "deepnote/install_nginx",
        "deepnote/nginx-meshcat-proxy.conf",
    ],
    visibility = ["//bindings/pydrake:__subpackages__"],
)

install_files(
    name = "install",
    dest = "share/drake/setup",
    files = select({
        "//tools/cc_toolchain:apple": [
            "mac/binary_distribution/Brewfile",
            "mac/binary_distribution/install_prereqs.sh",
            "mac/binary_distribution/requirements.txt",
        ],
        "//tools/cc_toolchain:linux": [
            "deepnote/install_nginx",
            "deepnote/nginx-meshcat-proxy.conf",
            "install_prereqs",
            "ubuntu/packages-jammy-binary.txt",
            "ubuntu/packages-noble-binary.txt",
        ],
        "//conditions:default": [],
    }),
    strip_prefix = [
        "mac/binary_distribution",
    ],
    rename = {
        # TODO(jwnimmer-tri) This is only for macOS; once we rewrite macOS to
        # also not be a bash script, we can remove this.
        "share/drake/setup/install_prereqs.sh": "install_prereqs",
    },
)

drake_py_unittest(
    name = "install_prereqs_test",
    # flaky = True,  # Might experience subprocess race conditions in the test.
    data = PREREQUISITES_DATA + [
        ":install_prereqs",
    ],
    deps = [
        "@rules_python//python/runfiles",
    ],
)

add_lint_tests(
    python_lint_extra_srcs = [
        "install_prereqs",
    ],
)

# -*- python -*-

load("@drake//tools/skylark:drake_cc.bzl", "drake_cc_binary")
load("//tools/lint:lint.bzl", "add_lint_tests")
load("@drake//tools/install:install.bzl", "install")

drake_cc_binary(
    name = "simple_continuous_time_system",
    srcs = ["simple_continuous_time_system.cc"],
    add_test_rule = 1,
    deps = [
        "//systems/analysis:simulator",
        "//systems/framework:vector_system",
    ],
)

drake_cc_binary(
    name = "simple_discrete_time_system",
    srcs = ["simple_discrete_time_system.cc"],
    add_test_rule = 1,
    deps = [
        "//systems/analysis:simulator",
        "//systems/framework:vector_system",
    ],
)

drake_cc_binary(
    name = "simple_mixed_continuous_and_discrete_time_system",
    srcs = ["simple_mixed_continuous_and_discrete_time_system.cc"],
    add_test_rule = 1,
    deps = [
        "//systems/analysis:simulator",
        "//systems/framework:leaf_system",
    ],
)

# The :prod_models in these packages are installed as part of the Drake release
# process, and loaded into drake_visualizer's model database by default.
INSTALLED_MODEL_PACKAGES = [
    "//examples/acrobot",
    "//examples/atlas",
    "//examples/compass_gait",
    "//examples/kuka_iiwa_arm",
    "//examples/manipulation_station",
    "//examples/multibody/cart_pole",
    "//examples/pendulum",
    "//examples/planar_gripper",
    "//examples/pr2",
    "//examples/quadrotor",
    "//examples/rimless_wheel",
    "//examples/simple_four_bar",
    "//examples/zmp",
]

filegroup(
    name = "prod_models",
    data = [package + ":prod_models" for package in INSTALLED_MODEL_PACKAGES],
    visibility = ["//visibility:public"],
)

# TODO(jwnimmer-tri): Figure out which examples needs to be installed (see
# issues #4224 and #1766).
INSTALLED_EXAMPLES = [
    "//examples/kuka_iiwa_arm",
]

install(
    name = "install",
    visibility = ["//visibility:public"],
    deps = [
        package + ":install_data"
        for package in INSTALLED_MODEL_PACKAGES
    ] + [
        package + ":install"
        for package in INSTALLED_EXAMPLES
    ],
)

add_lint_tests()
